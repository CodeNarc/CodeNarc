plugins {
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'idea'
}

group = "org.codenarc"
archivesBaseName = "CodeNarc"
version = "1.0"

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
     maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

def groovyVersion = "2.3.11"
def slf4jVersion = "1.7.25"

dependencies {
    compile("org.codehaus.groovy:groovy-all:$groovyVersion")
    compile("org.codehaus.groovy:groovy-ant:$groovyVersion") {
        exclude group: 'org.codehaus.groovy'
    }

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile('org.gmetrics:GMetrics:1.0') {
        exclude group: 'org.codehaus.groovy'
    }

    // JUnit is only required if you are using the CodeNarc testing framework, e.g. AbstractRuleTestCase
    compileOnly 'junit:junit:4.12'

    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
    testCompile 'commons-cli:commons-cli:1.4'
    testCompile 'junit:junit:4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/main/groovy', 'src/main/java'] }
    }
}

test.maxParallelForks = 2

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

javadoc {
    if (JavaVersion.current().java8Compatible) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
ext {
    // These properties should be defined in "gradle.properties" within the user.gradle dir
    ossrhUsername = "undefined"
    ossrhPassword = "undefined"
}
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'CodeNarc'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The CodeNarc project provides a static analysis tool for Groovy code.'
                url 'http://codenarc.org'

                scm {
                    connection 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                    developerConnection 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                    url 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chrismair'
                        name 'Chris Mair'
                        email 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }
}
