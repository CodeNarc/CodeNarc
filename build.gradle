plugins {
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'idea'
    id 'net.researchgate.release' version '2.0.2'
}

group = "org.codenarc"
archivesBaseName = "CodeNarc"
version = "0.27.0"

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
     maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile "org.codehaus.groovy:groovy-xml:$groovyVersion"
    compile "org.codehaus.groovy:groovy-ant:$groovyVersion"
    compile 'log4j:log4j:1.2.14'
    compile('org.gmetrics:GMetrics:0.7') { transitive = false }
    compile 'junit:junit:4.8.1'

    testCompile 'commons-cli:commons-cli:1.2'
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/main/groovy', 'src/main/java'] }
    }
}

test.maxParallelForks = 2

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

javadoc {
    if (JavaVersion.current().java8Compatible) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'CodeNarc'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The CodeNarc project provides a static analysis tool for Groovy code.'
                url 'http://codenarc.org'

                scm {
                    connection 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                    developerConnection 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                    url 'scm:git:git@github.com:CodeNarc/CodeNarc.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chrismair'
                        name 'Chris Mair'
                        email 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }
}

release {
    tagPrefix = project.name
    requireBranch = '' // disabled
}
